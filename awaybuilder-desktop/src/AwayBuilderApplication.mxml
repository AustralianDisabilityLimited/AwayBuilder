<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:components="awaybuilder.view.components.*"
	frameRate="60" backgroundFrameRate="5"
	minWidth="1024" minHeight="720" 
	backgroundColor="0x333333"
	showStatusBar="false"
	usePreloader="false"
	invoke="invokeHandler(event)" 
	preinitialize="preinitializeHandler(event)"
	>
	
	<fx:Declarations>
	</fx:Declarations>
	
	<fx:Style source="desktop_styles.css"/>
	
	<components:CoreEditor id="coreEditor" width="100%" height="100%"/>
	
	<fx:Script><![CDATA[
		import awaybuilder.AwayBuilder;
		import awaybuilder.desktop.DesktopAppContext;
		import awaybuilder.desktop.controller.events.OpenFromInvokeEvent;
		import awaybuilder.desktop.view.components.UpdatePopup;
		import awaybuilder.utils.logging.AwayBuilderLogger;
		
		import flash.security.CertificateStatus;
		
		import mx.controls.Alert;
		import mx.events.CloseEvent;
		import mx.events.FlexEvent;
		import mx.events.ResizeEvent;
		import mx.managers.CursorManager;
		import mx.managers.PopUpManager;

        private var _context:DesktopAppContext;

		public var splashScreen:SplashScreen;
		
		private var version:Array;
		private var _updatePopup:UpdatePopup;
		
		private var airLoader:URLLoader;
		
		private var _updateCancelled:Boolean = false;
		
        private function preinitializeHandler(event:FlexEvent):void
        {
			splashScreen = new SplashScreen();
            this._context = new DesktopAppContext(DisplayObjectContainer(this.systemManager));
			
			var urlLoader:URLLoader = new URLLoader();
			urlLoader.load( new URLRequest( "https://raw.github.com/awaytools/AwayBuilder/master/awaybuilder-core/src/awaybuilder/AwayBuilder.as" ) );
			urlLoader.addEventListener(Event.COMPLETE, urlLoader_completeHandler );
			urlLoader.addEventListener(IOErrorEvent.IO_ERROR, function(event:Event):void{ trace( event )} );
        }
		
		private function urlLoader_completeHandler( event:Event ):void
		{
			var urlLoader:URLLoader = event.target as URLLoader;
			var strings:Array = urlLoader.data.toString().split(";");
			
			var majorStrings:Array = strings[1].toString().split( " = " );
			var minorStrings:Array = strings[2].toString().split( " = " );
			var revisionStrings:Array = strings[3].toString().split( " = " );
			
			version = [parseInt( majorStrings[1] ), parseInt( minorStrings[1] ), parseInt( revisionStrings[1] )];
			
			var currentVersionWeight:Number = AwayBuilder.MAJOR_VERSION*100000+AwayBuilder.MINOR_VERSION*10000+AwayBuilder.REVISION;
			var versionWeight:Number = version[0]*100000+version[1]*10000+version[2];
			if( versionWeight > currentVersionWeight ) 
			{
				_updatePopup = UpdatePopup.show();
				_updatePopup.addEventListener( CloseEvent.CLOSE, versionAlert_closeHandler );
			}
			
		}
		private function versionAlert_closeHandler( event:CloseEvent ):void
		{
			if( event.detail == Alert.OK )
			{
				airLoader = new URLLoader();
				airLoader.dataFormat = URLLoaderDataFormat.BINARY;
				airLoader.load( new URLRequest( "https://github.com/awaytools/AwayBuilder/blob/master/awaybuilder-desktop/bin-release/AwayBuilderApplication.air?raw=true" ) );
				airLoader.addEventListener(Event.COMPLETE, airLoader_completeHandler );
				airLoader.addEventListener(ProgressEvent.PROGRESS, airLoader_progressHandler );
				airLoader.addEventListener(IOErrorEvent.IO_ERROR, airLoader_errorHandler );
			}
			else if( event.detail == Alert.CANCEL )
			{
				_updateCancelled = true;
				_updatePopup = null;
			}
		}
		
		private function airLoader_errorHandler( event:IOErrorEvent ):void
		{
			Alert.show( "Update interrupted, try next time" );
		}
		
		private function airLoader_progressHandler( event:ProgressEvent ):void
		{
			if( _updatePopup )
			{
				_updatePopup.setProgress( event.bytesLoaded, event.bytesTotal );
			}
		}
		private function airLoader_completeHandler( event:Event ):void
		{
			if( _updateCancelled ) return;
			var urlLoader:URLLoader = event.target as URLLoader;
			
			var file:File = File.applicationStorageDirectory.resolvePath("awayBuilderInstaller.air");
			var stream:FileStream = new FileStream();
			stream.open(file, FileMode.WRITE);
			stream.writeBytes( urlLoader.data );
			stream.close();
			file.openWithDefaultApplication();
			this.nativeApplication.exit();
		}

		
        private function invokeHandler(event:InvokeEvent):void
        {
            if(event.arguments.length == 1)
            {
                const extensions:Vector.<String> = new <String>["awd"];
                var filePath:String = event.arguments[0];
                var file:File = new File(filePath);
                if(file.exists && extensions.indexOf(file.extension) >= 0)
                {
                    this._context.eventDispatcher.dispatchEvent(new OpenFromInvokeEvent(OpenFromInvokeEvent.OPEN_FROM_INVOKE, file));
                }
            }
        }
		
	]]></fx:Script>
	
</s:WindowedApplication>